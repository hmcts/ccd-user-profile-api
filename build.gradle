buildscript {
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.6.RELEASE")
    }
    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
    }
    dependencies {
        classpath "gradle.plugin.com.github.spotbugs:gradlePlugin:1.6.0"
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
    }
}

plugins {
    id 'application'
    id 'io.spring.dependency-management' version '1.0.4.RELEASE'
    id 'org.springframework.boot' version '1.5.6.RELEASE'
    id 'org.owasp.dependencycheck' version '3.1.1'
    id 'se.patrikerdes.use-latest-versions' version '0.2.0'
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'org.sonarqube' version '2.6.2'
    id 'jacoco'
    id 'checkstyle'
    id "com.github.kt3k.coveralls" version "2.8.2"
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: "org.sonarqube"
apply plugin: "com.github.spotbugs"
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: "com.github.kt3k.coveralls"
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'se.patrikerdes.use-latest-versions'

mainClassName = 'uk.gov.hmcts.ccd.UserProfileApplication'

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'dependency-check-suppressions.xml'
}

// tag::repositories[]
repositories {
    maven {
        url "http://artifactory.reform.hmcts.net/artifactory/libs-release"
    }
    mavenLocal()
    jcenter()
    mavenCentral()
    maven {
        url  "https://dl.bintray.com/hmcts/hmcts-maven"
    }
}
// end::repositories[]

group 'uk.gov.hmcts.ccd'
version '1.2.0-SNAPSHOT'

// tag::jar[]
jar {
    baseName = 'user-profile'
    manifest {

        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version
    }

    configurations {
        jar.archiveName = 'user-profile.jar'
    }
}

configurations {
    providedRuntime
    compile.exclude(group: 'ch.qos.logback')

    aatCompile.extendsFrom(testCompile)
    aatRuntime.extendsFrom(testRuntime)
}
// end::jar[]

// tag::dependencies[]
sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    reformLogging= '2.0.2'
    junitVersion = '4.12'
    junitJupiterVersion = '5.1.0'
    junitVintageVersion = '5.1.0'
    junitPlatformVersion = '1.1.0'
    hibernateVersion = '5.2.12.Final'
    limits = [
            'instruction': 86,
            'branch'     : 88,
            'line'       : 88,
            'complexity' : 88,
            'method'     : 88,
            'class'      : 88
    ]
}

dependencies {
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-appinsights', version: reformLogging
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-httpcomponents', version: reformLogging
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-spring', version: reformLogging
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    compile(group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc') {
        exclude group: 'org.apache.tomcat', module: 'tomcat-jdbc'
    }
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.8.0'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.8.0'
    compile group: 'org.liquibase', name: 'liquibase-core', version: '3.5.5'
    compile group: 'com.zaxxer', name: 'HikariCP', version: '2.7.8'
    runtime group: 'org.postgresql', name: 'postgresql', version: '42.2.2'
    compile group: 'javax.inject', name: 'javax.inject', version: '1'
    compile group: 'uk.gov.hmcts.reform.auth', name: 'auth-checker-lib', version: '2.1.1'
    compile group: 'uk.gov.hmcts.auth.provider.service', name: 'service-token-generator-spring', version: '2.1.0'
    compile group: 'io.rest-assured', name: 'rest-assured', version: '3.0.7'
    testCompile "org.junit.platform:junit-platform-suite-api:${junitPlatformVersion}"
    testCompile "org.junit.platform:junit-platform-runner:${junitPlatformVersion}"
    testCompile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testRuntime "org.junit.platform:junit-platform-launcher:${junitPlatformVersion}"
    testRuntime "org.junit.vintage:junit-vintage-engine:${junitVintageVersion}"
    testCompile "junit:junit"
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testCompile group: 'com.opentable.components', name: 'otj-pg-embedded', version: '0.11.3'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.15.0'
    testCompile 'io.github.openfeign:feign-jackson:9.6.0'
    testCompile group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '1.2.4.RELEASE'
}
// end::dependencies[]

// tag::wrapper[]
task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}
// end::wrapper[]

task projectVersion {
    description 'Prints the version of this project; used for publishing JAR file'
    doLast {
        print project.version
    }
}

checkstyle {
    maxWarnings = 0

    toolVersion = '8.3'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configDir = new File(rootDir, 'config/checkstyle')
}

checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleTest)

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

// adopted from
// https://github.com/springfox/springfox/blob/fb780ee1f14627b239fba95730a69900b9b2313a/gradle/coverage.gradle
jacocoTestReport {
    reports {
        // XML required by coveralls and for the below coverage checks
        // and html are generated by default
        xml.enabled true
    }

    doLast {
        def report = file("${jacoco.reportsDir}/test/jacocoTestReport.xml")
        logger.lifecycle("Checking coverage results: ${report}")

        def parser = new XmlParser()
        parser.setFeature("http://apache.org/xml/features/nonvalidating/load-external-dtd", false);
        parser.setFeature("http://apache.org/xml/features/disallow-doctype-decl", false)
        def results = parser.parse(report)

        def percentage = {
            def covered = it.'@covered' as Double
            def missed = it.'@missed' as Double
            ((covered / (covered + missed)) * 100).round(2)
        }

        def counters = results.counter
        def metrics = [:]
        metrics << [
                'instruction': percentage(counters.find { it.'@type'.equals('INSTRUCTION') }),
                'branch'     : percentage(counters.find { it.'@type'.equals('BRANCH') }),
                'line'       : percentage(counters.find { it.'@type'.equals('LINE') }),
                'complexity' : percentage(counters.find { it.'@type'.equals('COMPLEXITY') }),
                'method'     : percentage(counters.find { it.'@type'.equals('METHOD') }),
                'class'      : percentage(counters.find { it.'@type'.equals('CLASS') })
        ]


        def failures = []
        metrics.each {
            def limit = limits[it.key]
            if (it.value < limit) {
                failures.add("- ${it.key} coverage rate is: ${it.value}%, minimum is ${limit}%")
            }
        }

        if (failures) {
            logger.quiet("------------------ Code Coverage Failed -----------------------")
            failures.each {
                logger.quiet(it)
            }
            logger.quiet("---------------------------------------------------------------")
            throw new GradleException("Code coverage failed")
        } else{
            logger.quiet("Passed Code Coverage Checks")
        }
    }
}

test {
    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "some-key")
}


sourceSets {
    aat {
        java {
            srcDir('src/aat/java')
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
        resources {
            srcDir('src/aat/resources')
        }
    }
}
task smoke(type: Test) {
    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "some-key")
    logger.quiet("TODO: Add smoke tests")
//    group = 'Delivery pipeline'
//    description = 'Executes functional tests against an AAT UserProfile instance'
//    setTestClassesDirs(sourceSets.aat.output.classesDirs)
//    setClasspath(sourceSets.aat.runtimeClasspath)
//    include 'uk.gov.hmcts.ccd.userprofile.tests.smoke'
//    useJUnitPlatform()

}

task functional(type: Test) {
    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "some-key")
    logger.quiet("TODO: Add functional tests")
//    group = 'Delivery pipeline'
//    description = 'Executes functional tests against an AAT CCD UserProfile Instance'
//    setTestClassesDirs(sourceSets.aat.output.classesDirs)
//    setClasspath(sourceSets.aat.runtimeClasspath)
//    include "uk/gov/hmcts/ccd/userprofile/tests/functional/**"
//    useJUnitPlatform()
}

check.dependsOn jacocoTestReport

sonarqube {
    properties {
        property "sonar.projectName", "ccd-user-profile-api"
        property "sonar.projectKey", "ccd-user-profile-api"
    }
}

spotbugsAat {
    ignoreFailures = true
}

task codeCoverageReport(type: JacocoReport, dependsOn: allprojects*.test) {

    // Gather execution data from all subprojects
    // (change this if you e.g. want to calculate unit test/integration test coverage separately)
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects
    allprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        csv.enabled = false
        html.enabled = true
        html.destination = file("${buildDir}/reports/jacoco/html")
        xml.enabled = true
        xml.destination = file("${buildDir}/reports/jacoco/jacocoTestReport.xml")
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'user-profile-api'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hmcts/user-profile'
        version {
            name = project.version
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}
